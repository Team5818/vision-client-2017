buildscript {
    repositories {
        mavenCentral()
        maven {
            name "central snapshots"
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    dependencies {
        classpath group: 'com.techshroom', name: 'Aversion', version: '3.0.0'
    }
}
plugins {
    id "com.google.protobuf" version "0.8.1"
    id "com.github.hierynomus.license" version "0.13.1"
    id 'net.researchgate.release' version '2.5.0'
    id "com.github.johnrengelman.shadow" version "1.2.4"
}

// from release-files
apply plugin: 'edu.sc.seis.launch4j'
apply plugin: 'application'

apply plugin: 'aversion-apt'
apply plugin: 'aversion-util'
apply plugin: 'idea'

mainClassName = 'org.rivierarobotics.VisionClient'

util {
    javaVersion = '1.8'
    extraContainers.add('org.eclipse.fx.ide.jdt.core.JAVAFX_CONTAINER')
}
idea.project.languageLevel = util.javaVersion

eclipse.project {
    natures 'org.eclipse.xtext.ui.shared.xtextNature'
    buildCommand 'org.eclipse.xtext.ui.shared.xtextBuilder'
}

configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor 0, 'seconds'
    }
}

repositories {
    mavenCentral()
    maven {
        name "central snapshots"
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

ext.protobufVersion = '3.2.0'
dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: project.protobufVersion
    //compile group: 'org.jcodec', name: 'jcodec-javase', version: '0.1.9'
    compileOnly group: 'com.techshroom', name: 'jsr305-plus', version: '0.0.1'

    addAPTReqWComp group: 'com.google.guava', name: 'guava', version: '20.0'
    addAPT group: 'com.google.auto.service', name: 'auto-service', version: '1.0-rc3'
    addAPT group: 'com.google.auto.value', name: 'auto-value', version: '1.3'
    addAPTReq name: 'aopalliance'
    addAPTReq name: 'asm'
    addAPTReq name: 'auto-value'
    addAPTReq name: 'auto-common'
    addAPTReq name: 'cglib'
    addAPTReq name: 'commons-collections'
    addAPTReq name: 'commons-lang'
    addAPTReq name: 'dagger'
    addAPTReq name: 'javawriter'
    addAPTReq name: 'javax.inject'
    addAPTReq name: 'velocity'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:' + project.protobufVersion
    }
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

configure([compileJava, compileTestJava]) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-path']
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.incremental = true
    options.fork = true
}

license {
    ext {
        name = project.name
        organization = project.organization
        url = project.url
    }
    header = rootProject.file('HEADER.txt')
    ignoreFailures = false
    strictCheck = true
    include '**/*.java'
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

task pullProtos() {
    doLast {
        def branch = project.findProperty('visionBranch') ?: 'master'
        def proto = "https://raw.githubusercontent.com/Team5818/vision-2017/${branch}/packet.proto".toURL().text
        def p = file('src/main/proto')
        p.mkdirs()
        def f = new File(p, 'packet.proto')
        f.delete()
        f << proto
    }
}

// hack in generate files for eclipse
eclipse.classpath.file {
    withXml {
        def node = it.asNode()
        def attrNode = node.appendNode('classpathentry', ['kind': 'src', 'path': 'build/generated/source/proto/main/java'])
            .appendNode('attributes');
        attrNode.appendNode('attribute', ['name': 'ignore_optional_problems', 'value': 'true']);
        attrNode.appendNode('attribute', ['name': 'optional', 'value': 'true']);
    }
}

import java.util.jar.Attributes.Name

jar {
    manifest {
        def map = new HashMap<String, String>()
        map.put(Name.MAIN_CLASS.toString(), project.property('mainClassName') as String)
        map.put(Name.IMPLEMENTATION_VERSION.toString(), project.version as String)
        attributes.putAll(map)
    }
}

apply plugin: me.kenzierocks.releasefiles.ReleaseFilesPlugin
